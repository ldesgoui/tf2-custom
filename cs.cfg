/// State
alias +cs.respawn               "cs.held"
alias -cs.respawn               "cs.noop"
alias cs.current                "cs.noop"
alias cs.success                "cs.noop"

/// State Machine
alias cs.held                   "cs.held.failure; cs.current.freeze; cs.class.random"
alias cs.held.success           "alias cs.success cs.noop; alias -cs.respawn cs.released"
alias cs.held.failure           "alias cs.success cs.held.success; alias -cs.respawn cs.noop"
alias cs.held.medic             "alias -cs.respawn cs.noop; load_itempreset 0"

alias cs.released               "cs.released.failure; cs.current.thaw; cs.current"
alias cs.released.success       "alias cs.success cs.noop; alias +cs.respawn cs.held"
alias cs.released.failure       "alias cs.success cs.released.success; alias +cs.respawn cs.released.recover"
alias cs.released.recover       "alias cs.success cs.noop; alias -cs.respawn cs.noop; alias +cs.respawn cs.held; cs.current"

alias cs.current.thaw           "alias cs.1 cs.current.1; alias cs.2 cs.current.2; alias cs.3 cs.current.3; alias cs.4 cs.current.4; alias cs.5 cs.current.5; alias cs.6 cs.current.6; alias cs.7 cs.current.7; alias cs.8 cs.current.8; alias cs.9 cs.current.9"
alias cs.current.freeze        "alias cs.1 cs.noop; alias cs.2 cs.noop; alias cs.3 cs.noop; alias cs.4 cs.noop; alias cs.5 cs.noop; alias cs.6 cs.noop; alias cs.7 cs.noop; alias cs.8 cs.noop; alias cs.9 cs.noop"

alias cs.current.1              "alias cs.current cs.class.1; alias +cs.respawn cs.held"
alias cs.current.2              "alias cs.current cs.class.2; alias +cs.respawn cs.held"
alias cs.current.3              "alias cs.current cs.class.3; alias +cs.respawn cs.held"
alias cs.current.4              "alias cs.current cs.class.4; alias +cs.respawn cs.held"
alias cs.current.5              "alias cs.current cs.class.5; alias +cs.respawn cs.held"
alias cs.current.6              "alias cs.current cs.class.6; alias +cs.respawn cs.held"
alias cs.current.7              "alias cs.current cs.class.7; alias +cs.respawn cs.held.medic"
alias cs.current.8              "alias cs.current cs.class.8; alias +cs.respawn cs.held"
alias cs.current.9              "alias cs.current cs.class.9; alias +cs.respawn cs.held"

/// Utils
alias cs.noop                   ""
alias cs.class.1                "joinclass scout"
alias cs.class.2                "joinclass soldier"
alias cs.class.3                "joinclass pyro"
alias cs.class.4                "joinclass demoman"
alias cs.class.5                "joinclass heavyweapons"
alias cs.class.6                "joinclass engineer"
alias cs.class.7                "joinclass medic"
alias cs.class.8                "joinclass sniper"
alias cs.class.9                "joinclass spy"
alias cs.class.random           "joinclass random"

/// In each class config
alias cs.scout                  "cs.success; cs.1"
alias cs.soldier                "cs.success; cs.2"
alias cs.pyro                   "cs.success; cs.3"
alias cs.demoman                "cs.success; cs.4"
alias cs.heavyweapons           "cs.success; cs.5"
alias cs.engineer               "cs.success; cs.6"
alias cs.medic                  "cs.success; cs.7"
alias cs.sniper                 "cs.success; cs.8"
alias cs.spy                    "cs.success; cs.9"
